Imports: {
	import static gate.Utils.*;
	import BHI.ReportExtractor.JapeRhsProcessor;
}


Phase: TagVarSentence
Input: Sentence Anatomy
Options: control=All negationGrouping=false
/*
 * Description:
 * Tag sentences that refer to the aortic root in general +/- the sentence after it 
 * as long as it doesn't contain other anatomical words
 */
Rule: var_sentence
(
	{Sentence contains {Anatomy.minorType == "aortic_root"}}
	({Sentence notContains {Anatomy.minorType != "aortic_root"}})?
			
):var_sentence
-->
:var_sentence.VarSentence = {type = "aortic_root"}



Phase: AortaVisualDiameter
Input: Anatomy Numeric Units Lookup Token VarSentence Categorical Split
Options: control=Appelt negationGrouping=false

Macro: CONTEXT
(
	{Anatomy,
		Anatomy within {VarSentence.type == "aortic_root"},
		Anatomy.minorType == "aortic_root"}
	({Lookup.minorType == "conjunction"} | {Lookup.minorType == "preposition"} | {Token.string=="/"} | {Token.string==","})?
)

Macro: CONTEXT_OTHER_AORTA
(
	{Anatomy,
		Anatomy within {VarSentence.type == "aortic_root"},
		Anatomy.majorType == "aorta"}
	({Lookup.minorType == "conjunction"} | {Lookup.minorType == "preposition"} | {Token.string=="/"} | {Token.string==","})?
)

Macro: FILTER
(
	{Token,
		Token.string != ",", //don't allow commas that aren't captured as "conjunctions" in the context
		!Anatomy, //don't read over anatomy not captured in context
		!Categorical,
		!Split.kind == "external",
		Token within {VarSentence.type == "aortic_root"}}
)

Macro: VALUE
(
	({Categorical, 
		Categorical.minorType == "dilated", 
		Categorical within {VarSentence.type == "aortic_root"}}) |
	({Categorical, 
		Categorical.minorType == "nondilated",
		Categorical within {VarSentence.type == "aortic_root"}})
)

Macro: MODIFIER
(
	{Categorical, 
		Categorical.minorType == "negative_modifier",
		Categorical within {VarSentence.type == "aortic_root"}}
)

/*
 * Description:
 * Of the forms...
 * e.g. 'root is not dilated for height'
 * e.g. 'the root and Asc ao are not dilated'
 */
Rule: ao_root_cat_1
Priority: 100
(
	(
		(CONTEXT (CONTEXT_OTHER_AORTA)[0,2]) |
		((CONTEXT_OTHER_AORTA)[0,2] CONTEXT) |
		((CONTEXT_OTHER_AORTA)[0,1] CONTEXT (CONTEXT_OTHER_AORTA)[0,1])
	):context
	
	( FILTER )[0,10]
			
	( (MODIFIER)? ):modifier
	
	( FILTER )[0,5]

	( VALUE ):value
	
):ao_root_cat 
--> 
:ao_root_cat
{
	/* Creation of the processor object does an 'in-place' processing of the annotations found using the above rules.
	 * Using the bindings, strings are extracted from the doc, processed/parsed, then inserted into the outputAS (output
	 * annotation set).	
	 */
	JapeRhsProcessor processor = new JapeRhsProcessor("echocardiogram", "ao_root_cat", doc, bindings, outputAS);
}


/*
 * Description:
 * Of the forms...
 * e.g. 'Non-dilated root'
 * e.g. 'Not dilated at the root'
 */
Rule: ao_root_cat_2
Priority: 99
(
	( (MODIFIER)? ):modifier
	
	( FILTER )[0,2]

	( VALUE ):value
	
	( FILTER )[0,3]
	
	(
		(CONTEXT (CONTEXT_OTHER_AORTA)[0,2]) |
		((CONTEXT_OTHER_AORTA)[0,2] CONTEXT) |
		((CONTEXT_OTHER_AORTA)[0,1] CONTEXT (CONTEXT_OTHER_AORTA)[0,1])
	):context
	
):ao_root_cat 
--> 
:ao_root_cat
{
	/* Creation of the processor object does an 'in-place' processing of the annotations found using the above rules.
	 * Using the bindings, strings are extracted from the doc, processed/parsed, then inserted into the outputAS (output
	 * annotation set).	
	 */
	JapeRhsProcessor processor = new JapeRhsProcessor("echocardiogram", "ao_root_cat", doc, bindings, outputAS);
}
