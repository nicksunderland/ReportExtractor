Imports: {
	import static gate.Utils.*;
	import BHI.ReportExtractor.JapeRhsProcessor;
}



Phase: BlockingAorticRootDiameter
Input: Token Anatomy Numeric Units Lookup VarSentence Split Categorical
Options: control=Appelt negationGrouping=false
/*
 * Description:
 * Specific blockers go here, generic blockers can go in the base grammar file.
 * Block general references to the proximal aorta followed by a more 
 * specific reference to part of the proximal aorta BUT the value-context 
 * is reversed such that the normal rules do not block it.
 * e.g. 'aortic root non-dilated (33mm at Sov level)'
 * e.g. 'non-dilated aortic root (33mm Sov level)'
 */
Rule: ao_root_blocker_1
Priority: 101
(	
	( {Anatomy,
		Anatomy within {VarSentence.minorType == [descriptor suffix=""]},
		Anatomy.minorType == [descriptor suffix=""]} )
	
	({Categorical})[0,2]
			
	{Token.kind == "punctuation", Token.position == "startpunct"}

	(
		( {Numeric, 
			Numeric within {VarSentence.minorType == [descriptor suffix=""]},
			Numeric.type == "double"} |
		  {Numeric,
			Numeric within {VarSentence.minorType == [descriptor suffix=""]}, 
			Numeric.type == "integer"} )
		
		( {Units,
			Units within {VarSentence.minorType == [descriptor suffix=""]},
			Units.majorType == "length"} )?
					
		( {Lookup.minorType == "preposition"} )?
		( {Anatomy.majorType == "aorta"} )
		( {Token} )[0,1]
		( {Lookup.minorType == "conjunction"} | {Lookup.minorType == "preposition"} | {Token.string=="/"} | {Token.string==","} )?
	)[0,3]
			
	{Token.kind == "punctuation", Token.position == "endpunct"}
			
):root_blocker
-->
:root_blocker.Blocked = {type = [descriptor suffix="_ao_diam"], rule = "ao_root_blocker_1"}

/*
 * Description:
 * Specific blockers go here, generic blockers can go in the base grammar file.
 * Block general references to the proximal aorta followed by a more 
 * specific reference to part of the proximal aorta.
 * e.g. 'Aortic root 4.2cm at sinus of valsalva'
 * e.g. 'Aortic root not dilated 4,2cm at the level of the sinuses'
 * e.g. 'Aortic root measures 4,2cm at the level of the sinuses'
 */
Rule: ao_root_blocker_2
Priority: 100
(	
	( {Anatomy,
		Anatomy within {VarSentence.minorType == [descriptor suffix=""]},
		Anatomy.minorType == [descriptor suffix=""]} )
	
	( {Token} )[0,4]

	( {Numeric, 
		Numeric within {VarSentence.minorType == [descriptor suffix=""]},
		Numeric.type == "double"} |
	  {Numeric,
		Numeric within {VarSentence.minorType == [descriptor suffix=""]}, 
		Numeric.type == "integer"} )
		
	( {Units,
		Units within {VarSentence.minorType == [descriptor suffix=""]},
		Units.majorType == "length"} )?
	
	{Token.string ==~ "(?i)at"}
	
	( {Token} )[0,4]
	
	( {Anatomy.majorType == "aorta"} )
			
):root_blocker
-->
:root_blocker.Blocked = {type = [descriptor suffix="_ao_diam"], rule = "ao_root_blocker_2"}
