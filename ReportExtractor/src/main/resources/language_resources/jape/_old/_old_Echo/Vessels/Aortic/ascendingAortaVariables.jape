Imports: {
	import static gate.Utils.*;

}


/*
*  ascendingAortaVariables.jape
*
* Copyright (c) 2020- , University of Bristol, Bristol.
*  Tim Dong, 31 January 2020 
*  $Id: ascendingAortaVariables.jape,v 1.0 2020/01/31 19:24:00 GMT
*
* The Phase separates Annotations of ascending aorta Variable relations from Lookup Annotations 
*/

Phase: ascendingAortaVariables
Input: Token Lookup  
Options: control=Appelt 


  
Rule: AscendingAortaValueRule
(
(
{Token.string ==~ "(?i)ASCENDING"}
({Token.string ==~ "(?i)AORTA"})

({Token.string == "="} | {Token.string == "["} | {Token.string == ":"})?
):context

(
({Token.kind == "number"})?
({Token.string == "."})?
{Token.kind == "number"}):varValue

(
(({Token.string == "cm"} | {Token.string == "mm"}):unit)
)

):AscendingAortaValueGram 
(
{Token.string != "/"}
(({Token.string !~ "(?i)M"} | {Token.string !~ "(?i)M2"})
{Token.string !~ "(?i)2"})?
)

--> 
:AscendingAortaValueGram
  {
	gate.AnnotationSet matchedVar=(gate.AnnotationSet) bindings.get("varValue");
	
	gate.AnnotationSet matchedcontext=(gate.AnnotationSet) bindings.get("context");
	
	gate.AnnotationSet matchedunit=(gate.AnnotationSet) bindings.get("unit");
	
    gate.AnnotationSet matchedAnns= (gate.AnnotationSet) bindings.get("AscendingAortaValueGram");	
	gate.FeatureMap newFeatures= Factory.newFeatureMap();
	newFeatures.put("varType","Ascending Aorta");
	
	String unitOrig = "";
	
	if (matchedunit != null) {
		unitOrig = stringFor(doc, matchedunit);
		newFeatures.put("unit", unitOrig);
	}
	
	String varStr = stringFor(doc, matchedVar);
	String contextStr = stringFor(doc, matchedcontext);
	
	double varDouble = Double.parseDouble(varStr);
	
	if (unitOrig.equals("mm")) {
	
		varDouble = varDouble / 10D;
		
		newFeatures.put("varOrig", varStr);
		newFeatures.put("varValue", String.valueOf(varDouble));
		
		newFeatures.put("unit", "cm");
		
	} else {
		newFeatures.put("varValue", varStr);
	}
	
	newFeatures.put("context", stringFor(doc, matchedcontext));
	
	outputAS.add(matchedAnns.firstNode(),matchedAnns.lastNode(),"VesselsMeasurements", newFeatures);
  }
 
