Imports: {
	import static gate.Utils.*;

}


/*
*  aorticRegurgitationVmaxVariables.jape
*
* Copyright (c) 2020- , University of Bristol, Bristol.
*  Tim Dong, 31 January 2020 
*  $Id: aorticRegurgitationVmaxVariables.jape,v 1.0 2020/01/31 19:24:00 GMT
*
* The Phase separates Annotations of aortic Regurgitation Vmax (LVOT and Desc Aorta level) Variable relations from Lookup Annotations 
*/

Phase: aorticRegurgitationVmaxVariables
Input: Token Lookup  
Options: control=Appelt 

 
 
Rule: DescAortaARVmaxValueRule
(
(
({Token.string !=~ "(?i)LEFT", Token.string !=~ "(?i)RIGHT"} {Token.string ==~ "(?i)AV[A]?"} | {Token.string ==~ "(?i)AORTA"} | 
({Token.string ==~ "(?i)AORTIC"} {Token.string ==~ "(?i)VALVE"} ))
({Token.string !=~ "(?i)AORTA", !Lookup.majorType == aortic_TricuspidValve_Exclusion, Token.string !=~ "(?i)LAVV", Token.string !=~ "(?i)RAVV", Token.string !=~ "(?i)LEFT", Token.string !=~ "(?i)LV", Token.string !=~ "(?i)RV", Token.string !=~ "(?i)RIGHT", Token.string !=~ "(?i)VENTRICLE"}|{SpaceToken.string!=VENTRICLE} | {Token.string ==~ "(?i)DESCENDING"} {Token.string ==~ "(?i)AORTA"})*
):context
)

(
(
({Token.string ==~ "(?i)FLOW"} {Token.string ==~ "(?i)REVERSAL"} | {Token.string ==~ "(?i)REVERSAL"} {Token.string ==~ "(?i)FLOW"} )

({Token.string !=~ "(?i)AV", Token.string !=~ "(?i)AO", Token.string !=~ "(?i)LVOT", Token.string !=~ "(?i)SINUS(ES)?", Token.string != ":", Token.string !~ "(?i)RIGHT", Token.string !~ "(?i)VENTRICLE", Token.string !~ "(?i)TRICUSPID", Token.string !~ "(?i)ATRIUM"}|{SpaceToken.string!=VTI})*

({Token.position == "startpunct"})?
({Token.string ==~ "(?i)VM[A]?X"} | {Token.string ==~ "(?i)VELOCITY"})

({Token.string == "="} | {Token.string == "["} | {Token.string == ":"} | {Token.string ==~ "(?i)IS"} | {Token.string == " "})?
):context

(({Token.kind == "number"})?
({Token.string == "."})?
{Token.kind == "number"}):varValue

(
(({Token.string ==~ "(?i)M"} | {Token.string ==~ "(?i)CM"}):unit)
)

):DescAortaARVmaxValueGram 

--> 
:DescAortaARVmaxValueGram
  {
	gate.AnnotationSet matchedVar=(gate.AnnotationSet) bindings.get("varValue");
	
	gate.AnnotationSet matchedcontext=(gate.AnnotationSet) bindings.get("context");
	
	gate.AnnotationSet matchedunit=(gate.AnnotationSet) bindings.get("unit");
	
    gate.AnnotationSet matchedAnns= (gate.AnnotationSet) bindings.get("DescAortaARVmaxValueGram");	
	gate.FeatureMap newFeatures= Factory.newFeatureMap();
	
	String unitOrig = "";
	
	if (matchedunit != null) {
		unitOrig = stringFor(doc, matchedunit);
		newFeatures.put("unit", unitOrig);
	}
	
	String varStr = stringFor(doc, matchedVar);
	
	double varDouble = Double.parseDouble(varStr);
	
	if (unitOrig.equals("m")) {
	
		varDouble = varDouble * 100D;
		
		newFeatures.put("varOrig", varStr);
		newFeatures.put("varValue", String.valueOf(varDouble));
		
		newFeatures.put("unit", "cm");
		
	} else {
		newFeatures.put("varValue", varStr);
	}
	
	newFeatures.put("varType","dAo AR Vmax");
	newFeatures.put("rule","dAo AR Vmax Normal");
	newFeatures.put("context", stringFor(doc, matchedcontext));
	
	outputAS.add(matchedAnns.firstNode(),matchedAnns.lastNode(),"VesselsMeasurements", newFeatures);
  }
  

Rule: AorticRegurgVmaxValueRule
(
(
{Token.string ==~ "(?i)AR"}
{Token.string ==~ "(?i)Vmax"}

({Token.string == "="} | {Token.string == "["} | {Token.string == ":"} | {Token.string ==~ "(?i)IS"} | {Token.string == " "})?
):context

(({Token.kind == "number"})?
({Token.string == "."})?
{Token.kind == "number"}):varValue

(
(({Token.string ==~ "(?i)M"} | {Token.string ==~ "(?i)CM"}):unit)
)

):AorticRegurgVmaxValueGram 

--> 
:AorticRegurgVmaxValueGram
  {
	gate.AnnotationSet matchedVar=(gate.AnnotationSet) bindings.get("varValue");
	
	gate.AnnotationSet matchedcontext=(gate.AnnotationSet) bindings.get("context");
	
	gate.AnnotationSet matchedunit=(gate.AnnotationSet) bindings.get("unit");
	
    gate.AnnotationSet matchedAnns= (gate.AnnotationSet) bindings.get("AorticRegurgVmaxValueGram");	
	gate.FeatureMap newFeatures= Factory.newFeatureMap();
	
	String unitOrig = "";
	
	if (matchedunit != null) {
		unitOrig = stringFor(doc, matchedunit);
		newFeatures.put("unit", unitOrig);
	}
	
	String varStr = stringFor(doc, matchedVar);
	
	double varDouble = Double.parseDouble(varStr);
	
	if (unitOrig.equals("cm")) {
	
		varDouble = varDouble / 100D;
		
		newFeatures.put("varOrig", varStr);
		newFeatures.put("varValue", String.valueOf(varDouble));
		
		newFeatures.put("unit", "m");
		
	} else {
		newFeatures.put("varValue", varStr);
	}
	
	newFeatures.put("varType","AR Vmax");
	newFeatures.put("rule","AR Vmax Altern");
	newFeatures.put("context", stringFor(doc, matchedcontext));
	
	outputAS.add(matchedAnns.firstNode(),matchedAnns.lastNode(),"VesselsMeasurements", newFeatures);
  }
  


  
