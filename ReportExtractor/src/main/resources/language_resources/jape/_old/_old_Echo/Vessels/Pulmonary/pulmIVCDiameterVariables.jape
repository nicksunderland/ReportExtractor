Imports: {
	import static gate.Utils.*;

}


/*
*  pulmIVCDiameterVariables.jape
*
* Copyright (c) 2020- , University of Bristol, Bristol.
*  Tim Dong, 31 January 2020 
*  $Id: pulmIVCDiameterVariables.jape,v 1.0 2020/01/31 19:24:00 GMT
*
* The Phase separates Annotations of Inferior Vena Cava Diameter Variable relations from Lookup Annotations 
*/

Phase: pulmIVCDiameterVariables
Input: Token Lookup  
Options: control=Appelt 


  
Rule: PulmIVCDiameterValueRule
(
(
({Token.string ==~ "(?i)INFERIOR"})?
(
{Token.string ==~ "(?i)VENA"}
{Token.string ==~ "(?i)CAVA"} |
{Token.string ==~ "(?i)IVC"}) 

({Token.string == "="} | {Token.string == "["} | {Token.string == ":"} | {Token.string ==~ "(?i)IS"} | {Token.string ==~ "(?i)OF"} | {Token.string == ","})?
({Token.string ==~ "(?i)NOT"} | {Token.string ==~ "(?i)NON[-]?"})?
({Token.string == "-"} | {Token.string ==~ "(?i)NON-DILATED"})?
({Token.string ==~ "(?i)DILATED"})?
({Token.kind == "word"})?
({Token.position == "startpunct"})?
({Token.kind == "word"})?
({Token.kind == "word"})?

({Token.string == "="} | {Token.string == "["} | {Token.string == ":"} | {Token.string ==~ "(?i)IS"} | {Token.string ==~ "(?i)OF"} | {Token.string == " "})?
):context

(({Token.kind == "number"})?
({Token.string == "."} | {Token.string == ","})?
{Token.kind == "number"}):varValue

(
(({Token.string ==~ "(?i)MM"} | {Token.string ==~ "(?i)CM"}):unit)
)

):PulmIVCDiameterValueGram 

--> 
:PulmIVCDiameterValueGram
  {
	gate.AnnotationSet matchedVar=(gate.AnnotationSet) bindings.get("varValue");
	
	gate.AnnotationSet matchedcontext=(gate.AnnotationSet) bindings.get("context");
	
	gate.AnnotationSet matchedunit=(gate.AnnotationSet) bindings.get("unit");
	
    gate.AnnotationSet matchedAnns= (gate.AnnotationSet) bindings.get("PulmIVCDiameterValueGram");	
	gate.FeatureMap newFeatures= Factory.newFeatureMap();
	
	String unitOrig = "";
	
	if (matchedunit != null) {
		unitOrig = stringFor(doc, matchedunit);
		newFeatures.put("unit", unitOrig);
	}
	
	String varStr = stringFor(doc, matchedVar);
	
	varStr = varStr.replace(",", ".");
	
	double varDouble = Double.parseDouble(varStr);
	
	if (unitOrig.equals("mm")) {
	
		varDouble = varDouble / 10D;
		
		newFeatures.put("varOrig", varStr);
		newFeatures.put("varValue", String.valueOf(varDouble));
		
		newFeatures.put("unit", "cm");
		
	} else {
		newFeatures.put("varValue", varStr);
	}
	
	newFeatures.put("varType","IVCd");
	newFeatures.put("context", stringFor(doc, matchedcontext));
	newFeatures.put("rule", "IVCd normal");
	
	outputAS.add(matchedAnns.firstNode(),matchedAnns.lastNode(),"VesselsMeasurements", newFeatures);
  }


